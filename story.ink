# theme: dark
# author: Ал Торо

LIST skills = hide_and_seek, tag, truth_or_dare

-> rules

=== game_over ===
    
    # CLASS: end
    КРАЙ!
    
-> END

=== rules ===
    
    #IMAGE: images/cover.png

    Главната героиня в тази история е Милена, 32-годишна фармацевтка. Преди да започнеш разказа, трябва единствено да избереш нейната любима детска игра, в която е била наистина вманиачена – <b>„Гоненица“</b>, <b>„Криеница“</b> или <b>„Истина или предизвикателство“</b> (позната още като „Дързост или откровение“).
        
    * Гоненица[.]
        
        ~ skills += tag
    
    * Криеница[.]
        
        ~ skills += hide_and_seek
    
    * Истина или предизвикателство[.]
    
        ~ skills += truth_or_dare
        
    -
    
    <> e твоята любима игра. А сега...
    
    * [Начало на историята!]
        -> intro
    
-> END

=== intro ===
//1

    Знаех, че е след мен. Забелязах го в парка още преди месец, преди храстите и дърветата да се раззеленят и да разцъфнат и хората да запъплят на тълпи из Борисовата градина. Вървеше често подире ми и винаги свиваше рязко в произволна посока, когато спирах да го изчакам. Няколко пъти, когато нямаше накъде да иде, дори тръгваше на обратно. Сменяше облеклото си и носеше качулка или шапка с козирка и очила, но винаги можех да го позная дори и само по осанката и начина на движение. Беше едър, атлетичен и ходеше бързо. Явно с времето беше успял да научи посоката, в която живея, защото все по-бързо се появяваше при вечерните ми разходки и ме следваше като злокобна сянка.
    
    * [Продължи.]
    
    -
    
    Тази вечер влязох от съвсем друг вход на парка. 
    
    Надявах се, че няма да ме открие, и допреди минута вярвах, че е така. Но бях сгрешила, може би фатално, и появата му неприятно ме изненада. Беше се смрачило и опразнило, а той беше по петите ми и за първи път вървеше след мен и към края на парка. Раничката потракваше на гърба ми от бързия ход, но въпреки това той скъсяваше дистанцията и сега беше само на няколко десетки метра зад мен.
    
    * [Побягнах към къщи.]
        -> running
    
    * [Свърнах по малката алея вдясно, за да се завъртя из парка, с надеждата да изгуби следите ми.]
    
        //15
    
        Не исках да го водя към къщи, затова свърнах надясно и мислех да му се изплъзна в сумрака из по-затънтените пътечки на парка. Само след секунди обаче той тръгна по диагонал измежду дърветата да пресича пътя ми. Явно не плануваше да играе по моите правила, а сега дистанцията помежду ни беше дори по-малка отпреди.
        
        Коя детска игра избра за любима?
        
        ** {skills has tag} [Гоненица.]
        
            //8
            
            През целия си живот бях поддържала отлична физическа форма и сега изглеждаше подходящ момент да се възползвам от нея. Трябваше да му се измъкна и криенето не беше опция.
            
            *** [Продължи.]
                ->running
        
        ** {skills hasnt tag} [Различна от Гоненица.]
        
            //18
            
            Забавянето ми беше фатално. Той хукна към мен, аз също се затичах, но макар само на стотина метра, първите улици и сгради до парка сега бяха безкрайно далече. Искаше ми се поне да захвърля раницата, но знаех, че не мога да го направя.
            
            Чувах стъпките му все по-близо зад мен. В последния момент се извъртях, за да се защитя, но нямаше какво да сторя. Нападателят ме помете ме и ме събори, а ножът вече беше в ръката му.
            
            -> game_over
    
    * [Тръгнах към него, за да видя какво иска от мен.]
    
        //20
        
        Този път той не свърна на обратно, когато тръгнах към него. „Хубаво!“, казах си. Нямах намерение да оставя този нещастник да ме тормози.
        
        Той свали очилата си и двамата се гледахме право в очите, докато вървяхме един към друг.
        
        – Кой си ти? Какъв ти е проблемът?! – извиках му, когато вече бяхме само на няколко метра.
        
        Вместо отговор чух изщракването на ножа в ръката му. Мислех че съм готова и за това, но не бях.
        
        -> game_over
    
->END

= running
//5
    
    Забързах към къщи и дори преминах в лек крос, но когато видях, че и той подтичва, нап-раво зарязах всички преструвки и хукнах презглава. С бърз поглед през рамо установих, че и той е сторил същото.
    
    Първата улица до парка беше само на стотина метра, а след нея в стара кооперация се намираше и моята квартира. Бягах без да губя повече време в погледи назад – те нямаше да ми помогнат, а едно препъване или забавяне можеше да е фатално.
    
    Позволих си да надникна, чак когато пресякох улицата, а близостта на блока и светлината над входната площадка ми вдъхнаха увереност, че вече съм на сигурно. Така изглеждаше – преследвачът ми беше изчезнал.
    
    Загледах се към парка с ключове и чип в ръка, готова да вляза светкавично във входа, ако се наложи. Подготвена бях и да натисна вратата след себе си, защото проклетият механизъм я затваряше прекалено бавно, особено през студените дни – обикновено стигах до втория етаж, преди да чуя изщракването на езичето долу.
    
    Останах така няколко минути. Взирах се в мрака и вероятно той също се взираше в мен. Искаше ми се да вярвам, че преследвачът ми се е уплашил от това, че може да бъде видян от други и е побързал да избяга, но вътрешно в себе си знаех, че е някъде там и ме наблюдава.
    
    * [Продължи.]
    
    -
    
    #IMAGE: images/episode_05.png
    
    Прибрах се, заключих и внимателно изпразних съдържанието на раницата си. Пъхнах я заедно с дрехите си в пералнята и побързах да се мушна под душа. Адреналинът бушуваше в кръвта ми и изпитвах коктейл от емоции, който ме караше да се чувствам истински жива – уплашена, триумфираща, заинтригувана… възбудена. Медната чаша с Contemplations – мерло и малбек – беше с мен в банята. Нямах намерение да развалям малкия си ритуал заради този тип; дори сега имах повече нужда да се насладя на любимото си вино.
    
    * [Продъжи.]
        -> home
->END

=== home ===
//12
    
    На следващия ден се прибрах по-рано от работа. Дните все още бяха къси, а точно днес не исках здрачът да ме хване извън къщи. Възнамерявах по изключение да пропусна и вечерната разходка. Още не бях подготвена – трябваше ми още ден да се сдобия с всичко необходимо, за да се защитя.
    
    #IMAGE: images/episode_12.png
    
    Пуснах телевизора и преминах през каналите, докато намеря новинарска емисия. Водещата новина ми беше известна, но исках да чуя детайлите. Колекционера беше взел още една жертва миналата вечер в новата си ловна територия – Борисовата градина. В едно от затънтените кътчета на парка бе убито младо момиче. Тялото ѝ – открито в храстите от пенсионер и кучето му тази сутрин. От полицията отказваха детайли, но потвърдиха, че убийството е настъпило вследствие на няколко прецизни прободни рани и, разбира се, че липсва лявото ухо на жертвата. Нямаха съмнение, че това е дело на същия сериен убиец.
    
    * [Продължи.]
    
    -
    
    Следователи за пореден път даваха празни обещания, че ще направят всичко за разнищване на случая, ще разпитат всички свидетели и ще прегледат внимателно записите на всички камери в района, но едва ли вярваха наистина, че ще открият нещо – в тази част на парка почти нямаше осветление, да не говорим за камери. Призоваваха и гражданите да не излизат по тъмно, да бъдат нащрек, да докладват на полицията при най-малки съмнения. Сигурно в момента стапяха телефоните на некомпетентните оператори на 112 с всевъзможни глупости.
    
    Вбесени граждани протестираха за липсата на ефективни полицейски мерки след ТОЛКОВА много убийства, извършени от Колекционера из София. Някои настояваха за оставката на министъра, други за тази на кметицата, а трети искаха вечерен час за парковете, докато убиецът не бъде заловен.
    
    Сестрата на жертва разказваше през сълзи колко добро момиче е била и колко много са я обичали всичките ѝ приятели и съученици.
    
    * [Продължи.]
    
    -
    
    В този момент токът спря и настана мрак. Тези дни пак беше захладняло, а откак цените на газта и парното скочиха до небето, всички се топлеха само на ток и често се случваше в някои квартали мрежата да не издържи. В скапаната ни стара кооперация често падаха и бушоните. Погледнах моите над вратата и не беше от тях. Централното табло се намираше отвън, на няколко метра вляво от входната врата. Хазаинът ми беше дал ключове за него, когато се нанесох преди няколко месеца.
    
    * (switchboard) [Слязох да проверя таблото, преди съвсем да се е мръкнало.]
        
        //4
        
        В момента, в който открехнах вратата, разбрах своята грешка, но вече беше късно. Той ме изблъска навътре още преди да съм отворила напълно и влетя в коридора на апартамента ми от тъмнината. Беше силен, бърз и с нож в ръка.
        
        -> game_over
    
    * [Обадих се на някой да го направи.]
        
        //14
            
        Зачудих се на кого да звънна. Енергото, разбира се, не беше опция, а хазаинът ми живееше далече и беше възрастен. Като смислен вариант оставаше само да позвъня на някой съсед.

        Коя детска игра избра за любима?
            
        ** {skills has truth_or_dare} [Истина или предизвикателство.]
            
            //7
            
            Имаше един съсед, Дочо, който ми беше оставил телефона си в случай на нужда. Беше семеен, около четиридесетгодишен и бързо след като се нанесох забелязах как ме заглежда. Обичах да изграждам контакти и бях добра в това – знаех колко полезно е понякога да имаш правилния човек подръка. Да се запозная и спечеля симпатиите на Дочо ми беше изключително лесно и сега учтиво ме поздравяваше и махаше усмихнато всеки път, когато се разминавахме из входа.
            
            – Ало. Здравей, Миленче, кажи с какво мога да ти бъда полезен?
            
            – Здрасти. Абе Доче, можеш ли да слезеш до долу, сигурно пак е паднал централният бушон на входа?
            
            – А, не, нещо друго е, при нас си има ток… Ало?… Да не ти е паднал някой от бушоните в апартамента? Да дойда ли да погледна? Да не е станало някое късо?
            
            – Сигурно е това – отговорих. – Сега ще проверя, ако не се оправя, ще ти звънна.
            
            – Добре, няма проблеми, будни сме.
            
            Затворих.
            
            *** [Продължи.]
                -> front_door
            
        ** {skills hasnt truth_or_dare} [Различна от Истина или предизвикателство.] 
            
            //17
            
            Така и не се бях запознала с никой от съседите – повечето бяха пенсионери или изглеждаха скучни и невзрачни личности. Освен това не намирах за разумно много-много да контактувам с тях. По принцип не харесвах болшинството от хората и ги отбягвах.
            
            Сега обаче нямаше на кого да се обадя. Затова:
            
            *** [… слязох да проверя таблото.]
                -> switchboard
        
            *** [… излязох на терасата.]
                -> balcony
    
    * (balcony) [Поизлязох на терасата.]
        
        //24
        
        Излязох на терасата и огледах квартала и блока. Навсякъде светеше, с изключение на моя апартамент, а нито имах кой знае какво включено, че да падне бушон, нито имаше особена вероятност нещо да е дало на късо точно сега.
        
        ** [Продължи.]
            -> front_door
    
->END

=== front_door ===
//13

    Изругах на ум. Имах подозрения какво може да се е случило, затова взех най-големия си кухненски нож, който имаше впечатляващи размери и беше добре наточен. Сетне тихо приближих към входната вратата. Заслушах се. Нищо. Тишина. Погледнах към прага и видях, че в коридора на блока е тъмно, а нямах ключ за осветлението на входа вътре в апартамента.
    
    Направих още крачка напред, без да издавам и звук, и допрях ухо до вратата. Стори ми се, че дочувам едва-едва, на границата на доловимото, спотаено дишане.
    
    В следващия миг тежък удар издумтя по врата и инстинктивно отскочих на метър от нея. Последва втори удар, трети, блъскане с рамо. Дървената врата се гънеше и изглеждаше сякаш всеки момент ще стане на трески. Нямаше начин съседите да не са чули, ако си бяха вкъщи, но дали някой щеше да посмее да излезе?
    
    * [Отключих и се втурнах в атака, за да изненадам противника.]
        
        //2
        
        Нападението се оказа по-малко изненадващо, отколкото очаквах, а изненадата пък ми даде по-малко предимство. Отключих бързо между блъсканията, отворих и успях да нанеса първа удар, намушквайки сериозно противника. Раната може би беше дори смъртоносна, но не и такава, която да го неутрализира на момента.
        
        Той също бе въоръжен с нож и ме връхлетя, въпреки раната си, поваляйки ме в коридора на апартамента. Не ми позволи да нанеса втори удар – физическата му сила и тегло, както и злобата и решителността, която явно изпитваше, му дадоха решаващо предимство.
        
        -> game_over
    
    * [Продължих да чакам, в готовност да се отбранявам.]
    
    -
    
    //22
    
    След няколко секунди, които усетих като цяла вечност, ударите рязко престанаха. След миг тишина чух бързо отдалечаващи се стъпки първо по коридора на блока, а после и по стълбите. Допрях отново ухо до вратата и чух кънтящи разговори някъде от далечината, сякаш още от горния етаж. Бяха съседите…
    
    Прекарах неприятен половин час в обяснения как бившето ми гадже е луд за връзване и как повече няма да се повтори. Вярвах, че последната част беше истина.
    
    * [Продължи.]
    
    -
    
    Нощта прекарах в полусън и с барикадирана врата. На следващия ден, привечер, се бях подготвила с нови играчки – електрошоков пистолет „Taser Pulse“ и малка спринцовка с милиграм етрофин. Тази доза можеше да парализира и кон, а за човек последствията биха били смъртоносни без администриране на антидот. Време беше за обичайната ми вечерна разходка…
    
    Денят беше студен и бая подухваше. Слънцето се бе скрило и макар още да не беше съвсем тъмно, Борисовата сигурно се беше поопразнила. Очаквах ситуацията да му хареса… и той не ме разочарова. Не му отне много да се появи след мен и явно го направи без колебание и без да се старае много да се прикрива – картите бяха открити. Насочих се към по-безлюдните части на парка – онези без камери и особено напоследък – без минувачи по това време. Там, където знаех, че ще бъдем само двамата. В ловната територия.
    
    Наоколо вече не се виждаха хора и той започна да скъсява смело дистанцията. Наближавах една порутена стара тоалетна, която щеше да ми позволи да се скрия за момент от погледа му. Мислех да използвам това тактическо предимство по най-добрия възможен начин, но как точно зависеше от това какво оръжие щях да избера.
    
    Мислех да започна атаката си със:
    
    * [… спринцовка.]
    
        //6
        
        В момента, в който той зави забързано зад ъгъла на порутената сграда и започна да се оглежда къде съм, аз вече пристъпвах тихо зад гърба му. Подухващият вятър ми даваше известно предимство, но и така задачата да го достигна бързо и безшумно не беше никак лека.
        
        Коя детска игра избра за любима?
        
        ** {skills has hide_and_seek} [Криеница.]
            
            //21
                
            Преследвачът ми ме видя чак когато замахвах, но беше прекалено късно за него – иглата на спринцовката се заби във врата му и инжектирах достатъчно от съдържанието ѝ, преди да успее да реагира. Бързо отскочих назад и добре, че го сторих, защото той замахна към мен с нож. Острието разпори въздуха на сантиметър от мен. Отдалечих се на още няколко крачки, а той се хвана за врата, започнал да усеща изтръпването.
            
            С насмешка наблюдавах първо учудването му, после отчаяния му опит да ме докопа. Когато най-сетне се свлече на земята, беше мой ред да действам.
            
            *** [Продължи.]
                -> the_end
        
        ** {skills hasnt hide_and_seek} [Различна от Криеница.]
            
            //9
            
            В последния момент той успя да ме забележи и се извърна. Замахнах със спринцовката и дори го одрасках по врата, но нямаше как да инжектирам съдържанието. Той отби ръката ми и замахна с ножа, който стискаше в другата. Битката беше кратка, кървава и прекалено лесна за противника ми.
            
            -> game_over
    
    * [… нож.] 
        
        //11
        
        В момента, в който той зави забързано зад ъгъла на порутената сграда и започна да се оглежда къде съм, аз се хвърлих към него с кинжала си. Успях да го изненадам и намушкам два-три пъти. Той обаче хвана ръката ми, силен като горила, и не ми позволи да нанеса повече удари. В своята също държеше малък нож. Дори и ранен, той с лекота взе надмощие – бях се озовала в директна схватка, която нямаше как да спечеля.
        
        -> game_over
    
    * [… електрошок.]
        
        //16
        
        Фронтален изстрел с електрошоковия пистолет щеше да ми е най-лесен, затова реших просто да го изчакам на това закътано място. Когато той се появи зад ъгъла забързан и ме видя, без колебания хукна към мен с изваден нож в ръка. До последния момент придържах оръжието прикрито зад себе си. Стрелях, когато той беше на:
    
        ** [2-3 метра от мен.]
        
        ** [5-6 метра от мен.]
            
            //25
            
            В страха си да не го допусна прекалено наблизо стрелях прекалено рано. Не прецених добре дистанцията и електродите безпомощно виснаха пред него и се оплетоха. От изненадата противникът ми леко се забави и отклони встрани, но миг след това продължи с пълна сила и ме връхлетя с нож в ръка.
            
            -> game_over
    -
    
    // 10
            
    Бях проучила оръжието достатъчно добре и знаех, че обхватът му е малък. Изчаках хладнокръвно, вдигнах го в последния момент и стрелях:
    
    * [… право в лицето му.]
        
        //3
        
        Насочих електрошоковия пистолет към лицето му и стрелях, но той инстинктивно наклони и приведе глава встрани, още преди да натисна спусъка. Електродите минаха на сантиметри над лявото му рамо, а той продължи с пълна сила и ме връхлетя с нож в ръка, като не ми остави шанс за каквато и да е реакция.
        
        -> game_over
    
    * [… в торса му (беше облечен в тънък анорак нагоре).]
        
        //19
        
        Насочих оръжието право към гърдите му, които бях сигурна, че няма да пропусна от такова разстояние. Знаех също, че тънките дрехи няма да го предпазят. Миг преди да ме достигне, двете стрелички се забиха в него. Той се сгърчи, а аз се дръпнах ловко встрани от пътя на некоординираното му тяло. Изпусна ножа си и се срина на асфалта, а аз бързо изритах оръжието встрани в тревата. Приведох се с вече приготвена спринцовка и рязко я инжектирах във врата му, без да пускам спусъка на тейзъра. След това изчаках за кратко гърчовете да отслабнат и етрофинът да го парализира хубаво. Беше наслада да гледам безпомощния му и ужасѐн поглед и жалките му опити да се изправи с гънещите се крайници. Когато омекна съвсем, беше мой ред да действам.
        
        ** [Продължи.]
            
            -> the_end
    
    * [… в му краката му (носеше спортни къси панталони, покриващи до малко над коляното).]
        
        //23
        
        Насочих електрошоковия пистолет към оголените му крака и стрелях. Поне единият от електродите го перна или се заби, задържах спусъка и от оръжието се чу електрически пукот. За мое учудване и съжаление нямаше особен ефект. Противникът ми подскочи, по-скоро от изненада, отколкото от болка, все едно бях хвърлила пиратка в краката му. В следващия миг обаче продължи с пълна сила и решимост и ме връхлетя с нож в ръка.
        
        -> game_over

-> END

=== the_end ===
    
    //26
    
    #IMAGE: images/episode_26.png
    
    С огромни усилия го издърпах встрани от пътеката и после още няколко метра до прикритието на храстите.

    Кой си ти? Нечий опечален брат или любовник без вяра на полицията и правосъдната система? Фен на комиксите или книгите-игри, който е повярвал, че може да бъде супер-герой? Така или иначе, вече нямаше значение.
    
    Приведох се към него и забих бавно и внимателно кинжала си между ребрата му, като внимавах да не ме изплиска. Кръвта най-после бликна и през устата му, освен от раната. Приготвих плика и херметичната кутия встрани и се приведох със скалпела към дясното му ухо – реших, че случаят е особен и трябва по някакъв начин да отбележа това. Имаше безброй бенки, неравномерни извивки, мастни бучки и противни косми. Със сигурност щеше да е най-грозното ухо в колекцията ми, но пък носеше специално значение за мен.
    
    -> game_over
    
-> END